{
  lib,
  runCommand,
  fetchurl,
  writeText,
  writers,
  coreutils,
  dmenu,
  gnused,
  libnotify,
  xclip,
  xdotool,
  gawk,
}: let
  unicode-file = runCommand "unicode.txt" {} ''
    ${
      writers.writePython3 "generate.py" {flakeIgnore = ["E501" "E722"];} ''
        import csv

        with open("${
          fetchurl {
            url = "https://unicode.org/Public/UCD/latest/ucd/UnicodeData.txt";
            sha256 = "sha256-NgGOaGV/3LNIX2NmMP/oyFMuAcl3cD0oA/W4nWxf6vs=";
          }
        }", "r") as unicode_data:
            reader = csv.reader(unicode_data, delimiter=";")
            next(reader)  # skip first row containing \0
            for row in reader:
                codepoint = row[0]
                name = row[1]
                alternate_name = row[10]
                try:
                    print(chr(int(codepoint, 16)), codepoint, name, alternate_name, sep="    ")
                except:
                    continue
      ''
    } > $out
  '';
  kaomoji-file = writeText "kaomoji.txt" ''
    Â¯\(Â°_o)/Â¯    dunno lol shrug dlol
    Â¯\_(ãƒ„)_/Â¯    dunno lol shrug dlol
    ( Í¡Â° ÍœÊ– Í¡Â°)    lenny
    Â¯\_( Í¡Â° ÍœÊ– Í¡Â°)_/Â¯    lenny shrug dlol
    ( ï¾ŸÐ´ï¾Ÿ)    aaah sad noo
    ãƒ½(^o^)ä¸¿    hi yay hello
    (^o^:    ups hehe
    (^âˆ‡^)    yay
    â”—(ï½€çš¿Â´)â”›    angry argh
    ãƒ¾(^_^)    byebye!! bye
    <(^.^<) <(^.^)> (>^.^)> (7^.^)7 (>^.^<)    dance
    (-.-)Zzz...    sleep
    (âˆ©â•¹â–¡â•¹âˆ©)    oh noes woot
    (â•¯Â°â–¡Â°ï¼‰â•¯ â”»â”â”»    table flip
    (ã€ã‚œãƒ­ã‚œ)ã€    why woot
    (_ã‚œ_ã‚œ_)    gloom I see you
    à¼¼ à¼Žàº¶ à·´ à¼Žàº¶à¼½    sad
    (\/) (Â°,,,,Â°) (\/)    krebs
    â”³â”â”³ ãƒ½(à² Ù„Íœà² )ï¾‰    putting table back
    â”»â”â”»ï¸µ \(Â°â–¡Â°)/ ï¸µ â”»â”â”»    flip all dem tablez
    (`ï½¥Ï‰ï½¥Â´)    bear look
    á•¦(àºˆÙ„Íœàºˆ)á•¤    strong flex muscle bicep
    á•¦(Ã²_Ã³Ë‡)á•¤    strong flex muscle bicep
    (à¹‘>á´—<à¹‘)    excite
    (âˆ© ` -Â´)âŠƒâ”â”â˜†ï¾Ÿ.*ï½¥ï½¡ï¾Ÿ    wizard spell magic
    â—• â—¡ â—•    puss in boots big eye
    â‰‹â‰‹â‰‹â‰‹â‰‹Ì¯Ì«âŒ§Ì¯Ì«(Ë†â€¢Ì® Ì®â€¢Ë†)    nyan cat
    Ê•â€¢á´¥â€¢Ê”    bear
    (Ô¾É·Ô¾)    adventure time
    (â€á—¢â€)    happy yay
    (â‰§â—¡â‰¦)    happy yay
    ï¼¼(Âº â–¡ Âº )/    panic
    ð“‚º    penis
    ð“‚¸    penis
  '';
in
  # ref https://github.com/LukeSmithxyz/voidrice/blob/9fe6802122f6e0392c7fe20eefd30437771d7f8e/.local/bin/dmenuunicode
  writers.writeDashBin "unicodmenu" ''
    history_file=$HOME/.cache/unicodmenu
    PATH=${lib.makeBinPath [coreutils dmenu gnused libnotify xclip xdotool]}
    chosen=$(cat "$history_file" ${kaomoji-file} ${unicode-file} | dmenu -p unicode -i -l 10 | tee --append "$history_file" | sed "s/    .*//")

    [ "$chosen" != "" ] || exit

    echo "$chosen" | tr -d '\n' | xclip -selection clipboard

    if [ -n "$1" ]; then
      xdotool key Shift+Insert
    else
      notify-send --app-name="$(basename "$0")" "'$chosen' copied to clipboard." &
    fi
  ''
